plugins {
    id 'java'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

group 'me.piggypiglet'
version '3.0.0'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
    maven {
        url = 'https://repo.codemc.org/repository/maven-public/'
    }
}

dependencies {
    compileOnly 'org.jetbrains:annotations:+'
    implementation("com.google.inject:guice:4.2.2") {
        exclude group: "com.google.code.gson", module: "gson"
        exclude group: "com.google.guava", module: "guava"
    }
    implementation 'org.bstats:bstats-bukkit:1.5'
    compileOnly 'org.bukkit:bukkit:1.12.2-R0.1-SNAPSHOT'
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

shadowJar {
    doLast {
        ['1.12', '1.13', '1.14', '1.15'].each{dest ->
            copy {
                from "build/libs/RandomSpawn-${project.version}-all.jar"
                into "spigot/${dest}/plugins"
            }
        }
    }

    relocate 'org.bstats', 'me.piggypiglet.randomspawn.bstats'
    relocate 'com.google.inject', 'me.piggypiglet.randomspawn.guice'
    relocate 'javax.inject', 'me.piggypiglet.randomspawn.javax'
    relocate 'org.aopalliance', 'me.piggypiglet.randomspawn.aopalliance'
}

clean {
    delete fileTree('spigot/') {
        include '**/RandomSpawn*.jar'
        include '**/plugins/RandomSpawn/*'
    }
}

compileJava.options.encoding = 'UTF-8'